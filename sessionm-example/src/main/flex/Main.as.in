package {

import flash.display.StageAlign;
import flash.display.Sprite;
import flash.display.BitmapData;
import flash.display.Loader;
import flash.geom.Rectangle;
import flash.geom.Matrix;
import flash.text.TextField;
import flash.text.TextFormat;
import flash.events.Event;
import flash.events.IOErrorEvent;
import flash.events.MouseEvent;
import flash.net.URLRequest;

import ludia.sessionm.ActivityType;
import ludia.sessionm.SessionM;
import ludia.sessionm.data.Achievement;
import ludia.sessionm.event.AchievementEvent;
import ludia.sessionm.event.ActivityEvent;
import ludia.sessionm.event.SessionEvent;
import ludia.sessionm.event.UserEvent;

public class Main extends Sprite {

    private var sessionM:SessionM;

    // Replace these by your SessionM App IDs
    private var iosAppID:String = "$(IOS-APP-ID)";
    private var androidAppID:String = "$(ANDROID-APP-ID)";
    private var platform:String;


    public function Main()
    {
        // This extension is not supported in the AIR emulator
        if(!SessionM.isSupported) {
            throw new Error("SessionM is not supported on this platform!");
        } 

        sessionM = new SessionM();
        platform = sessionM.getPlatform();
        createUI();
    }

    /**
    * There are two versions that can be read : this extension version and
    * the underlying SessionM SDK version (which differs on Android and iOS)
    */
    public function getVersions():void {
        prependText("SDK: " + sessionM.getSDKVersion() + ", extension: " + sessionM.getExtensionVersion());
    }

    /**
    * Some olders mobile OS versions might not be supported by SessionM
    * This would not provoke errors but you can check for this in your app
    */
    public function isSupportedPlatform():void {
        prependText("Is supported platform: " + sessionM.isSupportedPlatform());
    }

    /**
    * Starting a session is mandatory before calling activity methods.
    */
    private function startSession():void {
        sessionM.addEventListener(SessionEvent.SESSION_STATE_CHANGED, sessionM_sessionStateChangedHandler);
        sessionM.addEventListener(UserEvent.USER_UPDATED, sessionM_userUpdatedHandler);
        sessionM.addEventListener(AchievementEvent.UNCLAIMED_ACHIEVEMENT, sessionM_unclaimedAchievementHandler);

        if (platform == "iOS") {
            sessionM.startSession(iosAppID);
        }
        else if (platform == "Android") {
            sessionM.startSession(androidAppID);
        }
        else {
            throw new Error("Unsupported platform, use isSupported property before creating a SessionM object");
        }
    }

    /**
    * When a session is started, you will receive SessionEvent.SESSION_STATE_CHANGED
    * and the UI is updated accordingly
    */
    protected function sessionM_sessionStateChangedHandler(event:SessionEvent):void {
        prependText("Session state updated: " + event.state);
	updateUI();
    }

    /**
     * When SessionM user data changes, you will receive UserEvent.USER_UPDATED
     */
    protected function sessionM_userUpdatedHandler(event:UserEvent):void {
        prependText("User updated: " + event.user.pointBalance + " mPOINTS, " + event.user.unclaimedAchievementCount + " unclaimed achievement(s)");
        updateUI();
    }

    /**
     * When a user can claim a new achievement, you will receive AchievementEvent.UNCLAIMED_ACHIEVEMENT
     */
    private function sessionM_unclaimedAchievementHandler(event:AchievementEvent):void {
        prependText("Unclaimed achievement: " + event.achievement.name);
        updateUI();
    }

    /**
     * When using custom achievements, this instructs SessionM that a user performed a specific action that would
     * eventually reward him with an achievement
     */
    public function logAction():void {
        sessionM.logAction("custom");
    }

    /**
     * You can retrieve user data at anytime
     */
    public function getUserInfo():void {
        prependText("User: " + sessionM.getUser());
        prependText("Unclaimed achievement: " + sessionM.getUnclaimedAchievement());
    }

    /**
     * Calling initActivity() with ActivityType.PORTAL opens the main SessionM overlay,
     * where the user can engage with various offers.
     * You can subscribe to events of the ActivityEvent class to perform some custom logic.
     */
    private function presentPortal():void {
        sessionM.addEventListener(ActivityEvent.ACTIVITY_DISMISSED, sessionM_activityDismissedHandler);
        sessionM.addEventListener(ActivityEvent.ACTIVITY_PRESENTED, sessionM_activityPresentedHandler);
        sessionM.addEventListener(ActivityEvent.ACTIVITY_UNAVAILABLE, sessionM_activityUnavailableHandler);
        sessionM.addEventListener(ActivityEvent.USER_ACTION, sessionM_userActionHandler);
        sessionM.initActivity(ActivityType.PORTAL);
    }

    /**
     * Calling initActivity() with ActivityType.ACHIEVEMENT opens an achievement popup,
     * if the user has non-native unclaimed achievements.
     * initCustomActivity() will be called instead if the user has native unclaimed achievements.
     * You can subscribe to events of the ActivityEvent class to perform some custom logic.
     */
    private function presentAchievement():void {
        sessionM.addEventListener(ActivityEvent.ACTIVITY_DISMISSED, sessionM_activityDismissedHandler);
        sessionM.addEventListener(ActivityEvent.ACTIVITY_PRESENTED, sessionM_activityPresentedHandler);
        sessionM.addEventListener(ActivityEvent.ACTIVITY_UNAVAILABLE, sessionM_activityUnavailableHandler);
        sessionM.addEventListener(ActivityEvent.USER_ACTION, sessionM_userActionHandler);

	var ach:Achievement = sessionM.getUnclaimedAchievement();

        if (ach.isCustom) {
           displayCustomAchievement(ach);
           prependText("displayCustomAchievement succeeded");
           sessionM.initCustomActivity();
        }
        else {
           sessionM.initActivity(ActivityType.ACHIEVEMENT);
       }
    }

    protected function sessionM_activityDismissedHandler(event:ActivityEvent):void {
        prependText("Activity dismissed");
    }

    protected function sessionM_activityPresentedHandler(event:ActivityEvent):void {
        prependText("Activity presented");
    }

    protected function sessionM_activityUnavailableHandler(event:ActivityEvent):void {
        prependText("Activity unavailable");
    }

    protected function sessionM_userActionHandler(event:ActivityEvent):void {
        prependText("User action: " + event.userAction);
    }

    private function clearLog():void {
        txtStatus.text = "";
    }

    private function prependText(text:String):void {
        txtStatus.text = text + "\n" + txtStatus.text;
    }


    /** Status */
    private var txtStatus:TextField;

    /** Buttons */
    private var buttonContainer:Sprite;

    /** Button layout */
    private var layout:ButtonLayout;
 
    /** Rewards badge */
    private var rewardsBadge:SimpleButton = new SimpleButton(new Command("REWARDS", presentPortal));

    /** Create UI */
    public function createUI():void
    {
        stage.align = StageAlign.TOP_LEFT;
        txtStatus=new TextField();
        txtStatus.defaultTextFormat=new TextFormat("Arial",20);
        txtStatus.width=stage.stageWidth;
        txtStatus.height=150;
        txtStatus.multiline=true;
        txtStatus.wordWrap=true;
        txtStatus.text="Press \"Start session\"";
        txtStatus.y = 10;
        addChild(txtStatus);

        buttonContainer=new Sprite();
        buttonContainer.y=txtStatus.height;
        addChild(buttonContainer);

        var uiRect:Rectangle=new Rectangle(0,0,stage.stageWidth,stage.stageHeight);
        layout=new ButtonLayout(uiRect,14);

        layout.addButton(new SimpleButton(new Command("Start session", startSession)));
        layout.addButton(new SimpleButton(new Command("Log Action", logAction)));
        layout.addButton(new SimpleButton(new Command("Present portal", presentPortal)));
        layout.addButton(new SimpleButton(new Command("Present achievement", presentAchievement)));
        layout.addButton(new SimpleButton(new Command("Get user info", getUserInfo)));
        layout.addButton(new SimpleButton(new Command("Is supported platform", isSupportedPlatform)));
        layout.addButton(new SimpleButton(new Command("Get versions", getVersions)));
        layout.addButton(new SimpleButton(new Command("Clear log", clearLog)));

        layout.update(buttonContainer);
    }

    /** Update UI */
    public function updateUI():void
    {
	// Should the rewards badge be displayed?
        if (sessionM.shouldDisplayRewardsBadge()) {
            prependText("Displaying rewards badge");

            // Show the badge if it is not already being shown
            var ach:String = sessionM.getUser().unclaimedAchievementCount.toString();
            rewardsBadge.label = "REWARDS " + ach;

            if (!buttonContainer.contains(rewardsBadge)) {
                prependText("Unclaimed achievements: " + ach);
                rewardsBadge.visible = true;
                layout.addButton(rewardsBadge);
                layout.update(buttonContainer);
            }
        } 
        else {
            prependText("Do not display rewards badge");

            // Hide the badge if it is being shown
            if (buttonContainer.contains(rewardsBadge)) {
		rewardsBadge.visible = false;
                layout.removeButton(rewardsBadge);
                layout.update(buttonContainer);
            }
        }
    }

    /** Display custom achievement */
    public function displayCustomAchievement(ach:Achievement):void {
        var achIcon:Sprite = new Sprite();
        var loader:Loader = new Loader();
        loader.load(new URLRequest(ach.achievementIconURL));
        loader.contentLoaderInfo.addEventListener(Event.COMPLETE, drawAchievement);
        loader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR, ioErrorHandler);
        achIcon.addEventListener(MouseEvent.CLICK, onClick);


        function drawAchievement(event:Event):void {
            var bitmap:BitmapData = new BitmapData(loader.width, loader.height, false);
            bitmap.draw(loader);

            achIcon.graphics.beginBitmapFill(bitmap);
            achIcon.graphics.drawRect(0, 0, loader.width, loader.height);
            achIcon.graphics.endFill();

            prependText("Presenting custom achievement");
            addChild(achIcon);
        }

        function ioErrorHandler(event:IOErrorEvent):void {
            prependText("Unable to load achievement icon");
        }

        function onClick(event:MouseEvent):void {
            prependText("Dismissing custom achievement");
            removeChild(achIcon);
            sessionM.notifyDismissedAchievement("CANCELED");
        }
    }
}
}
